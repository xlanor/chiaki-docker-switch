# Source: https://github.com/xfangfang/wiliwili/tree/5388a1035ba256839a7cb4c01686204b148b2cae/scripts/switch/dav1d
# Contributor:  averne <averne381@gmail.com>
_realname=dav1d
pkgname=switch-${_realname}
pkgver=1.2.1
pkgrel=1
pkgdesc="dav1d is an AV1 cross-platform decoder, open-source, and focused on speed and correctness."
arch=("any")
url="https://code.videolan.org/videolan/${_realname}"
license=("BSD")
options=(!strip libtool staticlibs)
source=(
  "${url}/-/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
  "dav1d.patch"
)

makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
groups=("switch-portlibs")

prepare() {
  cd ${_realname}-${pkgver}

  patch -Np1 -i "$srcdir/dav1d.patch"
}

build() {
  cd ${_realname}-${pkgver}

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt build -Dc_std=gnu11 -Db_ndebug=true -Denable_asm=true \
    -Denable_tools=false -Denable_examples=false -Denable_tests=false -Denable_docs=false
  meson compile -C build
}

package() {
  cd ${_realname}-${pkgver}

  DESTDIR="$pkgdir" meson install -C build
}
sha256sums=('2dd85860d213479672b1c708e31593446e8c2b53ff41e2ca25a2eafb718424e2'
            'dc93ce144129c67cb4b26bf67653bb1548184a9757edfa4a598535c3dd3a458e')